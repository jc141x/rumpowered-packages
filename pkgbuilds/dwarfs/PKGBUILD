pkgname=dwarfs
pkgver=0.14.1
pkgrel=1
pkgdesc="A fast high compression read-only file system"
url='https://github.com/mhx/dwarfs'
arch=('x86_64')
license=('GPL3 AND MIT')
depends=(
  'fuse3' 'openssl' 'boost-libs' 'jemalloc' 'xxhash' 'fmt'
  'lz4' 'xz' 'zstd' 'brotli' 'libarchive' 'flac'
  'libunwind' 'google-glog' 'fmt' 'gflags' 'double-conversion'
)
makedepends=(
  'cmake' 'boost' 'libevent'
  'libdwarf' 'chrono-date'
  'utf8cpp' 'range-v3' 'git'
  'gtest' 'nlohmann-json'
)
source=("$pkgname-$pkgver.tar.xz::https://github.com/mhx/dwarfs/releases/download/v$pkgver/dwarfs-$pkgver.tar.xz"
)
cksums=('3891070383')
sha256sums=('620cf27f2e142a5f8fc05552a70704c3bf4df23c3279c6026b3f37954d0529c5')
b2sums=('0aaba364533fbd992eb312492332a193171e3e4781820a0b2b574255a039a7da3ca365b7a929f2ce8511f84babf81ca30b93791fd6491f5b54b0cd49a17640ce')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=None \
    -D WITH_MAN_OPTION=OFF \
    -D PREFER_SYSTEM_GTEST=ON \
    -D CMAKE_CXX_FLAGS=-w \
    -D CMAKE_POLICY_VERSION_MINIMUM=3.5 # https://github.com/facebook/folly/pull/2398

  cmake --build build -j$(nproc)
}

#check() {
  #make test
#}

package() {
  cmake --install build --prefix "$pkgdir/usr"

  mv "$pkgdir/usr/sbin"/* "$pkgdir/usr/bin"
  rm -rf "$pkgdir/usr/sbin"

  install -Dm0644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

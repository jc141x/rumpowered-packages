pkgname=dwarfs
pkgver=0.12.2
pkgrel=1
pkgdesc="A fast high compression read-only file system"
url='https://github.com/mhx/dwarfs'
arch=('x86_64' 'aarch64')
license=('GPL3 AND MIT')
depends=(
  'fuse3' 'openssl' 'boost-libs' 'jemalloc' 'xxhash' 'fmt'
  'lz4' 'xz' 'zstd' 'brotli' 'libarchive' 'flac'
  'libunwind' 'google-glog' 'fmt' 'gflags' 'double-conversion'
)
makedepends=(
  'cmake' 'boost' 'libevent'
  'libdwarf' 'chrono-date'
  'utf8cpp' 'range-v3' 'git'
  'gtest' 'nlohmann-json'
)
source=("$pkgname-$pkgver.tar.xz::https://github.com/mhx/dwarfs/releases/download/v$pkgver/dwarfs-$pkgver.tar.xz"
)
cksums=('1761482803')
sha256sums=('9b256d1f2bc17917cd63a1bee3bd5f505076b4d880fcf9daa18a6ca5bca35aeb')
b2sums=('32ea6e400e5e87355031b934dce6590291ff395a65192537552474b7826bf23c212e1db7c41bced87d69afe9c70c9b59e889c54e3a9d9e3e24c07b45cb3a6e80')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=None \
    -D WITH_MAN_OPTION=OFF \
    -D PREFER_SYSTEM_GTEST=ON \
    -D CMAKE_CXX_FLAGS=-w \
    -D CMAKE_POLICY_VERSION_MINIMUM=3.5 # https://github.com/facebook/folly/pull/2398

  cmake --build build -j$(nproc)
}

#check() {
  #make test
#}

package() {
  cmake --install build --prefix "$pkgdir/usr"

  mv "$pkgdir/usr/sbin"/* "$pkgdir/usr/bin"
  rm -rf "$pkgdir/usr/sbin"

  install -Dm0644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

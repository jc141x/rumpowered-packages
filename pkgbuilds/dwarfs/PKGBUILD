pkgname=dwarfs
pkgver=0.10.1
pkgrel=3
pkgdesc="A fast high compression read-only file system"
url='https://github.com/mhx/dwarfs'
arch=('x86_64' 'aarch64')
license=('GPL3')
depends=(
  'fuse3' 'openssl' 'boost-libs' 'jemalloc' 'xxhash' 'fmt'
  'lz4' 'xz' 'zstd' 'brotli' 'libarchive' 'flac'
  'libunwind' 'google-glog' 'fmt' 'gflags' 'double-conversion'
)
makedepends=(
  'cmake' 'boost' 'libevent'
  'libdwarf' 'chrono-date'
  'utf8cpp' 'range-v3' 'git'
  'gtest' 'nlohmann-json'
)
source=("$pkgname-$pkgver.tar.xz::https://github.com/mhx/dwarfs/releases/download/v$pkgver/dwarfs-$pkgver.tar.xz"
"boost-process.diff::https://github.com/mhx/dwarfs/commit/6c8a1cdbd3c889c9cdf8bbbfe7c39cc9c25202a0.diff"
)
cksums=('1246522120'
        '1449900770')
sha256sums=('db785e0e0f257fa4363d90153db34127add4552791a72998b30ded787840d039'
            '855f462e56441c4786712042a7f0f0a55b7dde88ad8da448ddf0069603bd2782')
b2sums=('5885e7dc4092dd678383958839afcc02e906c95dcb8e2af2a649738a3d41196ec51f17a4362b413863136bd67cbd3d8225c147ed2da1a089561ec6e5d2587d4b'
        '2ec349542427a63c63aea6e88b2b50c34f9c86e651aeca7d1eb1786e329734b41eca4c9901951b0450eea47db09d57eb71568a761653e2a3c550a53921928449')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 < ../boost-process.diff
}

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=None \
    -D WITH_MAN_OPTION=OFF \
    -D PREFER_SYSTEM_GTEST=ON \
    -D CMAKE_CXX_FLAGS=-w

  cmake --build build -j$(nproc)
}

#check() {
  #make test
#}

package() {
  cmake --install build --prefix "$pkgdir/usr"

  mv "$pkgdir/usr/sbin"/* "$pkgdir/usr/bin"
  rm -rf "$pkgdir/usr/sbin"

  install -Dm0644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

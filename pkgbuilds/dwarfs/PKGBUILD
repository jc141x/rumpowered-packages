pkgname=dwarfs
pkgver=0.13.0
pkgrel=3
pkgdesc="A fast high compression read-only file system"
url='https://github.com/mhx/dwarfs'
arch=('x86_64')
license=('GPL3 AND MIT')
depends=(
  'fuse3' 'openssl' 'boost-libs' 'jemalloc' 'xxhash' 'fmt'
  'lz4' 'xz' 'zstd' 'brotli' 'libarchive' 'flac'
  'libunwind' 'google-glog' 'fmt' 'gflags' 'double-conversion'
)
makedepends=(
  'cmake' 'boost' 'libevent'
  'libdwarf' 'chrono-date'
  'utf8cpp' 'range-v3' 'git'
  'gtest' 'nlohmann-json'
)
source=("$pkgname-$pkgver.tar.xz::https://github.com/mhx/dwarfs/releases/download/v$pkgver/dwarfs-$pkgver.tar.xz"
"boost-system.patch"
)
cksums=('2828036738'
        '1216288171')
sha256sums=('d0654fcc1219bfd11c96f737011d141c3ae5929620cd22928e49f25c37a15dc9'
            '6b177a988b65a5c544ad90e4d1861bee1353a63fe368a7d8c956011625292b1a')
b2sums=('19cf0b68ffb016907cac88521ff5390f1ef637e978e4c9c5a32bf99fb9f7f9e4214b5b74c49dcd4b1fd1f3a7057b0bc9d1f821e871c4870cee0b41c186e5143e'
        'f401f94cb04a64120080d915733313fff72ffe1be6bdbe8cbb8ee33dc8c21f75ff60183176b1e5f06c92cdc9f8d656100fdba59b465aecd35f4e232b3b8d99b5')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np2 -i ../boost-system.patch
}

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=None \
    -D WITH_MAN_OPTION=OFF \
    -D PREFER_SYSTEM_GTEST=ON \
    -D CMAKE_CXX_FLAGS=-w \
    -D CMAKE_POLICY_VERSION_MINIMUM=3.5 # https://github.com/facebook/folly/pull/2398

  cmake --build build -j$(nproc)
}

#check() {
  #make test
#}

package() {
  cmake --install build --prefix "$pkgdir/usr"

  mv "$pkgdir/usr/sbin"/* "$pkgdir/usr/bin"
  rm -rf "$pkgdir/usr/sbin"

  install -Dm0644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

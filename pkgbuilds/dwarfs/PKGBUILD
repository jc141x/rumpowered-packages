pkgname=dwarfs
pkgver=0.13.0
pkgrel=3
pkgdesc="A fast high compression read-only file system"
url='https://github.com/mhx/dwarfs'
arch=('x86_64')
license=('GPL3 AND MIT')
depends=(
  'fuse3' 'openssl' 'boost-libs' 'jemalloc' 'xxhash' 'fmt'
  'lz4' 'xz' 'zstd' 'brotli' 'libarchive' 'flac'
  'libunwind' 'google-glog' 'fmt' 'gflags' 'double-conversion'
)
makedepends=(
  'cmake' 'boost' 'libevent'
  'libdwarf' 'chrono-date'
  'utf8cpp' 'range-v3' 'git'
  'gtest' 'nlohmann-json'
)
source=("$pkgname-$pkgver.tar.xz::https://github.com/mhx/dwarfs/releases/download/v$pkgver/dwarfs-$pkgver.tar.xz"
"boost-system.patch"
)
cksums=('2828036738'
        '1176073376')
sha256sums=('d0654fcc1219bfd11c96f737011d141c3ae5929620cd22928e49f25c37a15dc9'
            '16cda56849988cb5ab43e9f23e6d7cbffd3b7a33cfc9f5820a93ae0032ab1339')
b2sums=('19cf0b68ffb016907cac88521ff5390f1ef637e978e4c9c5a32bf99fb9f7f9e4214b5b74c49dcd4b1fd1f3a7057b0bc9d1f821e871c4870cee0b41c186e5143e'
        'fdc540abaf3e6170c0c94a0cdedf73a2f8e4c96f00ac1bb8a6eabfbb879ec72bdfe287373be5c72a4412e99a4858633301011e5d0f6b5b5dc40350a80d0c1638')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np2 -i ../boost-system.patch
}

build() {
  cmake -B build -S "$pkgname-$pkgver" \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_BUILD_TYPE=None \
    -D WITH_MAN_OPTION=OFF \
    -D PREFER_SYSTEM_GTEST=ON \
    -D CMAKE_CXX_FLAGS=-w \
    -D CMAKE_POLICY_VERSION_MINIMUM=3.5 # https://github.com/facebook/folly/pull/2398

  cmake --build build -j$(nproc)
}

#check() {
  #make test
#}

package() {
  cmake --install build --prefix "$pkgdir/usr"

  mv "$pkgdir/usr/sbin"/* "$pkgdir/usr/bin"
  rm -rf "$pkgdir/usr/sbin"

  install -Dm0644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
